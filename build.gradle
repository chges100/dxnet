buildscript {
    repositories {
        mavenCentral()

        maven {
            url "https://plugins.gradle.org/m2/"
        }

        maven {
            url "https://dl.bintray.com/hhu-bsinfo/dxram"
        }
    }

    dependencies {
        classpath "de.hhu.bsinfo:dxgradle:0.3.1"
    }
}

plugins {
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
    id "com.jfrog.artifactory" version "4.7.5"
}

apply plugin: 'dxgradle'
group = 'de.hhu.bsinfo'
version = '0.3.1' + (Boolean.valueOf(System.getProperty("snapshot")) ? "-SNAPSHOT" : "")

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

mainClassName = 'de.hhu.bsinfo.dxnet.DXNetMain'

// There doesn't seem to be a way get the APP_HOME variable to the path of config files relative
// to the application dir. Source:
// https://stackoverflow.com/questions/25227198/how-to-pass-a-reference-to-distribution-home-directory-using-gradle-application
project.startScripts.with {
    doLast {
        unixScript.text = unixScript.text.replaceFirst('(?<=DEFAULT_JVM_OPTS=)((\'|\")(.*)(\'|"))(?=\n)',
                '\'$3 "-Dlog4j.configurationFile=file:\\$APP_HOME/config/log4j2.xml"\'')
    }
}

repositories {
    mavenCentral()

    maven {
        url "https://dl.bintray.com/hhu-bsinfo/dxram"
    }

    maven {
        url 'https://oss.jfrog.org/artifactory/oss-snapshot-local'
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-Werror", "-Xlint:sunapi", "-XDenableSunApiLintControl"]
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

dependencies {
    implementation fileTree(dir: 'lib', include: ['*.jar'])

    implementation 'de.hhu.bsinfo:dxmon:0.3.1-SNAPSHOT'
    implementation 'de.hhu.bsinfo:dxutils:0.3.1-SNAPSHOT'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

apply from: 'types.gradle'
apply from: 'publish.gradle'
apply from: 'bintray.gradle'
apply from: 'artifactory.gradle'


task wrapper(type: Wrapper) {
    gradleVersion = "4.9"
}
